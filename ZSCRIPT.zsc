version "4.5"

#include "ZScript/MerchZForms/Include.zsc" // [Ace] For the menu.
#include "ZSCRIPT.MerchantMenu.zsc"
#include "ZSCRIPT.StoreItems.zsc"
#include "ZSCRIPT.MerchantSkins.zsc"
#include "ZSCRIPT.SlotMachine.zsc"
#include "ZSCRIPT.PlayingCards.zsc"
#include "ZSCRIPT.Poker.zsc"

class MerchantRewardStorage : Thinker
{
	int Money;

	static MerchantRewardStorage Get()
	{
		ThinkerIterator it = ThinkerIterator.Create('MerchantRewardStorage', STAT_STATIC);
		MerchantRewardStorage Storage;
		while ((Storage = MerchantRewardStorage(it.Next())))
		{
			return Storage;
		}

		// [Ace] Create a new one if none exist.
		Storage = new('MerchantRewardStorage');
		Storage.ChangeStatNum(Thinker.STAT_STATIC);
		return Storage;
	}
}

class MerchantStore play
{
	Array<StoreItem> StoreItems;
	Array<string> Categories;
}

class MerchantHandler : EventHandler
{
	Array<StoreCurrency> StoreCurrencies;
	MerchantStore Stores[3];
	int OpenedStoreIndex[MAXPLAYERS]; // [Ace] This is multiplayer bullshit.

	private MerchantRewardStorage RewardStorage;
	private int LastSecret;

	override void NetworkProcess(ConsoleEvent e)
	{
		let plr = players[e.Player].mo;
		if (e.Name ~== "HDM_AssemblePokerGame")
		{
			AssembleGame(plr);
			return;
		}

		if (e.Name ~== "HDM_PlaceBet")
		{
			PlaceBet(CardPlayers[PlayerIndex], e.Args[0]);
			return;
		}

		if (e.Name ~== "HDM_Check")
		{
			ActionTimer = 0;
			CardPlayers[PlayerIndex].HasChecked = true;
			CardPlayers[PlayerIndex].Player.A_StartSound("Poker/Check", 10, CHANF_UI, attenuation: 1.5);
			RotatePlayer();
			return;
		}

		Array<string> ParsedName;
		e.Name.Split(ParsedName, ":");
		if (ParsedName[0] ~== "HDM_BuyStuff")
		{
			for (int i = 0; i < Stores.Size(); ++i)
			{
				MerchantStore Store = Stores[i];
				StoreItem Item;
				for (int i = 0; i < Store.StoreItems.Size(); ++i)
				{
					if (Store.StoreItems[i].GetClass() == ParsedName[1])
					{
						Item = Store.StoreItems[i];
						break;
					}
				}

				if (Item && plr)
				{
					class<Actor> ItemClass = Item.GetItem();
					int Amount = Item.GetAmount();

					StoreCurrency Currency = StoreCurrencies[e.Args[0]];
					int Price = StoreItem.GetAdjustedPrice(Item, Currency);

					if (Price > plr.CountInv(Currency.GetCurrencyClass()))
					{
						plr.A_StartSound("Merchant/BuyFailure", 15, CHANF_LOCAL | CHANF_UI);
						return;
					}

					if (!(ItemClass is 'HDPickup') ||  ItemClass is 'HDPickupGiver')
					{
						Actor a; bool success;
						[success, a] = plr.A_SpawnItemEx(ItemClass, 6, 0, plr.height - 8, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(2.0, 5.0), 0, SXF_NOCHECKPOSITION);
						if (success && a is 'Inventory')
						{
							Inventory(a).Amount = amount;
						}
					}
					else
					{
						class<Inventory> InvClass = (class<Inventory>)(ItemClass);
						int MaxGive = GetDefaultByType(InvClass).MaxAmount - plr.CountInv(InvClass);
						int MaxActualGive = min(Amount, MaxGive);

						// [Ace] Give what you can directly and drop the rest on the ground.
						plr.A_GiveInventory(InvClass, MaxActualGive);
						Amount -= MaxActualGive;

						if (Amount > 0)
						{
							Actor a; bool success;
							[success, a] = plr.A_SpawnItemEx(InvClass, 6, 0, plr.height - 8, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(2.0, 5.0), 0, SXF_NOCHECKPOSITION);
							if (success)
							{
								Inventory(a).Amount = Amount;
							}
						}
					}
					plr.A_TakeInventory(Currency.GetCurrencyClass(), Price);
					plr.A_StartSound("Merchant/BuySuccess", 16, CHANF_LOCAL | CHANF_UI);

					Item.StoreAmount--;
					if (Item.StoreAmount == 0)
					{
						Store.StoreItems.Delete(Store.StoreItems.Find(Item));
						Item.Destroy();
					}
					break;
				}
			}
		}
	}

	override void WorldThingDied(WorldEvent e)
	{
		if (!random(0, 12) && hdm_enablebucks & 1 && e.Thing is 'HDOperator' && !e.Thing.bFRIENDLY)
		{
			e.Thing.A_SpawnItemEx('MercenaryCard', 0, 0, 0, frandom(0.5, 2.0), 0, frandom(1.0, 4.0), random(0, 359), SXF_NOCHECKPOSITION);
		}
	}

	void InitStore(int type)
	{
		if (!Stores[type])
		{
			Stores[type] = new('MerchantStore');
		}

		Stores[type].StoreItems.Clear();
		Stores[type].Categories.Clear();
		
		for (int i = 0; i < AllClasses.Size(); ++i)
		{
			if (AllClasses[i] is "StoreItem" && AllClasses[i] != 'StoreItem')
			{
				StoreItem Item = StoreItem(new(AllClasses[i]));
				class<Actor> a = Item.GetItem();

				bool inReloadingRoom = level.MapName ~== "LOTSAGUN";

				static const Name CVarNames[] = { 'hdm_chance_weapon', 'hdm_chance_ammo', 'hdm_chance_item' };
				if (a && Item.GetType() == type && (inReloadingRoom || frandom[merchrand](0.01, 1.00) <= CVar.GetCVar(CVarNames[type]).GetFloat()))
				{
					Item.StoreAmount = Item.GetStoreAmount();
					if (inReloadingRoom)
					{
						Item.StoreAmount *= random[merchrand](25, 150);
					}
					Stores[type].StoreItems.Push(Item);
				}
			}
		}

		for (int i = 0; i < Stores[type].StoreItems.Size(); ++i)
		{
			// [Ace] Don't duplicate categories.
			if (Stores[type].Categories.Find(Stores[type].StoreItems[i].GetCategory()) == Stores[type].Categories.Size())
			{
				Stores[type].Categories.Push(Stores[type].StoreItems[i].GetCategory());
			}
		}
	}

	void InitCurrencies()
	{
		for (int i = 0; i < AllClasses.Size(); ++i)
		{
			if (AllClasses[i] is "StoreCurrency" && AllClasses[i] != 'StoreCurrency')
			{
				StoreCurrency Currency = StoreCurrency(new(AllClasses[i]));
				class<Actor> a = Currency.GetCurrencyClass();
				if (a)
				{
					StoreCurrencies.Push(Currency);
				}
			}
		}
	}

	override void WorldLoaded(WorldEvent e)
	{
		if (e.IsReopen)
		{
			return;
		}

		RewardStorage = MerchantRewardStorage.Get();

		InitCurrencies();
		for (int i = 0; i < Stores.Size(); ++i)
		{
			InitStore(i);
		}

		Array<class<HDMerchant> > AvailableMerchants;
		AvailableMerchants.Push((class<HDMerchant>)('AmmoMerchant'));
		AvailableMerchants.Push((class<HDMerchant>)('ItemMerchant'));
		AvailableMerchants.Push((class<HDMerchant>)('WeaponMerchant'));

		int mCount = AvailableMerchants.Size();
		int MerchantsLeft = min(mCount, level.MapName ~== "LOTSAGUN" ? mCount : CVar.GetCVar('hdm_maxmerchants').GetInt());

		bool spawnedMachine = false;

		Array<Sector> validSecs;

		for (int i = 0; i < level.Sectors.Size(); ++i)
		{
			Sector sec = level.Sectors[i];
			if (!level.IsPointInLevel((sec.centerspot, sec.floorplane.ZAtPoint(sec.centerspot))))
			{
				continue;
			}
			if (sec.IsSecret() && random[merchrand](1, 100) <= max(15, 60 - 5 * (level.total_secrets - 1)) || sec.GetUDMFInt('user_merchantsector') == 1)
			{
				validSecs.Push(sec);
			}
			else if (!spawnedMachine && sec.IsSecret() && random[merchrand](1, 100) <= max(15, 50 - 5 * (level.total_secrets - 1)) && hdm_enableslots || sec.GetUDMFInt('user_slotmachinesector') == 1)
			{
				vector3 spawnPos = (sec.centerspot.x, sec.centerspot.y, sec.floorplane.ZAtPoint(sec.centerspot));
				Actor a = Actor.Spawn('MerchantSlotMachine', spawnPos);
				a.angle = sec.GetUDMFInt('user_slotmachineangle');
				spawnedMachine = true;
			}
		}

		Array<int> iteratedIndices;
		int vsize = validSecs.Size();
		while (vsize > 0 && iteratedIndices.Size() < vsize)
		{
			int i = random[merchrand](0, vsize - 1);
			if (iteratedIndices.Size() > 0 && iteratedIndices.Find(i) != iteratedIndices.Size())
			{
				continue;
			}
			iteratedIndices.Push(i);

			Sector sec = validSecs[i];
			if (MerchantsLeft > 0)
			{
				vector3 spawnPos = (sec.centerspot.x, sec.centerspot.y, sec.floorplane.ZAtPoint(sec.centerspot));
				int SpawnIndex = min(randompick(0, 0, 0, 1, 1, 2), AvailableMerchants.Size() - 1);
				Actor a = Actor.Spawn(AvailableMerchants[SpawnIndex], spawnPos);
				a.angle = sec.GetUDMFInt('user_merchantangle');
				AvailableMerchants.Delete(SpawnIndex);
				MerchantsLeft--;
			}
			else
			{
				break;
			}
		}
	}

	override void WorldUnloaded(WorldEvent e)
	{
		let plr = players[Net_Arbitrator].mo;
		if (!plr)
		{
			return;
		}

		for (int i = 0; i < MAXPLAYERS; ++i)
		{
			if (players[i].mo)
			{
				players[i].mo.A_TakeInventory("HDMerchantBody");
			}
		}

		Array<HDMerchant> EscortedMerchants;
		BlockThingsIterator it = BlockThingsIterator.Create(plr, HDCONST_ONEMETRE * 10);
		while (it.Next())
		{
			if (!(it.thing is 'HDMerchant') || plr.Distance3D(it.thing) > HDCONST_ONEMETRE * 10 || !plr.CheckSight(it.thing))
			{
				continue;
			}

			EscortedMerchants.Push(HDMerchant(it.thing));
		}

		RewardStorage = MerchantRewardStorage.Get();
		if (RewardStorage.Money > 0 && hdm_enablebucks & 4)
		{
			RewardStorage.Money = int(RewardStorage.Money * (1.0 + 0.2 * EscortedMerchants.Size()));
		}

		for (int i = 0; i < EscortedMerchants.Size(); ++i)
		{
			EscortedMerchants[i].Destroy(); // [Ace] You have escorted the merchant(s) to the exit. They won't be there if you return. Only affects hubs.
			if (hdm_enablebucks & 2)
			{
				RewardStorage.Money += 100;
			}
		}

		// [Ace] The process of rewarding players for their intense suffering.
		if (RewardStorage.Money > 0)
		{
			int moneyPerPlayer = int(RewardStorage.Money / GetPlayersInGame());
			for (int i = 0; i < MAXPLAYERS; ++i)
			{
				if (players[i].mo && MoneyPerPlayer > 0)
				{
					players[i].mo.A_GiveInventory("MercenaryBucks", moneyPerPlayer);
				}
			}
			RewardStorage.Money = 0;
		}
	}

	private int GetPlayersInGame()
	{
		int TotalPlayers = 0;
		for (int i = 0; i < MAXPLAYERS; ++i)
		{
			if (playeringame[i])
			{
				TotalPlayers++;
			}
		}
		return TotalPlayers;
	}

	override void WorldTick()
	{
		if (!RewardStorage)
		{
			RewardStorage = MerchantRewardStorage.Get();
		}
		if (level.found_secrets != LastSecret)
		{
			LastSecret = level.found_secrets;
			if (hdm_enablebucks & 4)
			{
				RewardStorage.Money += 20;
			}
		}

		if (GameInProgress)
		{
			PokerTick();
		}
	}
}

// ----------------------------------------
// MERCHANT
// ----------------------------------------

class HDMerchantBody : HDPickup
{
	Default
	{
		-INVENTORY.INVBAR
		+INVENTORY.UNDROPPABLE
		+HDPICKUP.NOTINPOCKETS
		HDPickup.Bulk 500;
		Inventory.MaxAmount 1;
	}
}

class HDMerchant : Actor abstract
{
	override void BeginPlay()
	{
		Super.BeginPlay();
		
		InitSkins();
		A_GiveInventory('MercenaryBucks', 1000);
	}

	override void Tick()
	{
		A_SpriteOffset(0, SOffsetY);

		if (PiggyBack)
		{
			A_Face(PiggyBack);
			Warp(PiggyBack, -12, 0, 14, flags: WARPF_NOCHECKPOSITION | WARPF_INTERPOLATE);
			bNOGRAVITY = true;
			bTHRUACTORS = true;
			bSOLID = false;

			Scale.Y = default.Scale.Y * PiggyBack.player.crouchfactor;

			if (PiggyBack.player.crouchfactor < 0.6 && PiggyBack.player.cmd.buttons & BT_SPEED || PiggyBack.incapacitated > 0 || PiggyBack.fallroll > 0)
			{
				if (DropHoldTimer++ == 16 || PiggyBack.incapacitated > 0)
				{
					bSOLID = true;
					bNOGRAVITY = false;
					bTHRUACTORS = false;
					bFORCEYBILLBOARD = false;
					PiggyBack.A_TakeInventory("HDMerchantBody");
					PiggyBack = null;
					Scale.Y = default.Scale.Y;
					DropHoldTimer = 0;
				}
			}
			else
			{
				DropHoldTimer = 0;
			}
		}
		else
		{
			PlayerIsNear = CheckProximity("PlayerPawn", HDCONST_ONEMETRE * 4, 1, CPXF_ANCESTOR | CPXF_SETTARGET);
			if (PlayerIsNear)
			{
				A_Face(target, 15);
			}
		}

		Super.Tick();
	}

	override bool Used(Actor user)
	{
		let plr = HDPlayerPawn(user);
		let handler = MerchantHandler(EventHandler.Find("MerchantHandler"));

		if (!plr || PiggyBack == plr || Distance3D(plr) > 48 || handler.ActorIsInPokerGame(self))
		{
			return false;
		}

		if (Pissed)
		{
			plr.A_Print("\c[Red]Fuck off! Store's closed, cheater!");
			return false;
		}

		if (plr.player.cmd.buttons & BT_SPEED && !plr.CheckInventory("HDMerchantBody", 1))
		{
			DropHoldTimer = 0;
			PiggyBack = plr;
			PlayerIsNear = false; // [Ace] This forces the merchant to cease their animation.
			plr.A_GiveInventory("HDMerchantBody");
			bFORCEYBILLBOARD = true;
			return false;
		}

		if (plr.PlayerNumber() == consoleplayer)
		{
			LastUser = plr;
			handler.OpenedStoreIndex[plr.PlayerNumber()] = ItemsType;
			Menu.SetMenu('MerchantShopMenu');
		}

		if (!PiggyBack && InStateSequence(CurState, FindState('SpawnLoop')))
		{
			TryPlaySpecialAnimation();
		}

		return true;
	}

	override bool CanCollideWith(Actor other, bool passive)
	{
		if (other is "PlayerPawn")
		{
			return Super.CanCollideWith(other, passive);
		}

		return false;
	}

	// [Ace] This is complete fucking overkill. It could have been done in like 5 lines of code but I wanted a more "generic" solution.
	protected void CheckBreathing()
	{
		if (BreatheStepHold > 0)
		{
			BreatheStepHold--;
			return;
		}

		if (BreatheHold > 0)
		{
			BreatheHold--;
			return;
		}

		if (BreatheIndex < BreatheStepsHalf)
		{
			A_SetScale(Scale.X, Scale.Y + BreatheScaleStep);
			BreatheStepHold = BreatheStepDelay;
			if (++BreatheIndex == BreatheStepsHalf)
			{
				BreatheHold = BreatheInOutDelay;
			}
		}
		else if (BreatheIndex >= BreatheStepsHalf)
		{
			A_SetScale(Scale.X, Scale.Y - BreatheScaleStep);
			BreatheStepHold = BreatheStepDelay;
			if (++BreatheIndex == BreatheSteps)
			{
				BreatheIndex = 0;
				BreatheHold = BreatheInOutDelay;
			}
		}
	}

	protected void BuyingIdle(StateLabel goAwayState, string snd)
	{
		CheckBreathing();
		if (!PlayerIsNear)
		{
			A_StartSound(snd, CHAN_VOICE);
			SetStateLabel(goAwayState);
		}
	}

	const BreatheInOutDelay = 30;
	const BreatheStepDelay = 4;
	const BreatheMaxScale = 0.03;

	// [Ace] Always go for even values. Never, ever go for odd values. Only villains do that. Higher values mean smoother animation.
	// This number is the number of frames from completely exhaled to completely inhaled, to exhaling.
	const BreatheSteps = 4;

	const BreatheScaleStep = BreatheMaxScale / BreatheSteps;
	const BreatheStepsHalf = BreatheSteps / 2;
	private int BreatheIndex;
	private int BreatheHold;
	private int BreatheStepHold;

	private bool PlayerIsNear;

	HDPlayerPawn PiggyBack;
	private HDPlayerPawn LastUser;
	bool Pissed;

	private double SOffsetY;

	private int DropHoldTimer;

	private HDMerchantSkin SelectedSkin;

	meta int ItemsType;
	property ItemsType: ItemsType;

	Default
	{
		Mass 400;
		Height 58;
		Radius 8;
		Scale 1.0;
		+SOLID
		+NOBLOCKMONST
	}
}

class WeaponMerchant : HDMerchant
{
	Default
	{
		Tag "Weapon Merchant";
		HDMerchant.ItemsType StoreItem.SType_Weapon;
	}
}

class AmmoMerchant : HDMerchant
{
	Default
	{
		Tag "Ammo Merchant";
		HDMerchant.ItemsType StoreItem.SType_Ammo;
	}
}

class ItemMerchant : HDMerchant
{
	Default
	{
		Tag "Item Merchant";
		HDMerchant.ItemsType StoreItem.SType_Item;
	}
}

class MercenaryBucks : HDAmmo
{
	override void SplitPickup()
	{
		int SplitAmount = min(Amount, random(100, 500));
		while (Amount > SplitAmount)
		{
			int SubSplit = min(Amount, random(100, 500));
			Actor a = Spawn("MercenaryBucks", pos);
			a.vel += vel + (frandom(-1, 1), frandom(-1, 1), frandom(-1, 1));
			Scale.X *= randompick(-1, 1);
			Inventory(a).Amount = SubSplit;
			Amount -= SubSplit;
		}

		if (Amount < 1)
		{
			Destroy();
			return;
		}
	}

	override bool IsUsed()
	{
		return true;
	}

	private int BuckFrame;

	Default
	{
		+FORCEXYBILLBOARD
		Tag "Bucks";
		Inventory.Icon "MBCKA0";
		+HDPICKUP.CHEATNOGIVE
		+HDPICKUP.MULTIPICKUP
		+HDPICKUP.FITSINBACKPACK
		-HDPICKUP.DROPTRANSLATION
		HDPickup.Bulk 0.07;
		HDPickup.RefID "bux";
		Inventory.MaxAmount 20000;
		Inventory.PickupMessage "Picked up some bucks.";
		Scale 0.4;
	}

	States
	{
		Spawn:
			MBCK A -1;
			Stop;
	}
}

class MercenaryCard : HDUPK
{
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();

		Amount = int(Amount * frandom(0.60, 1.20) * hdm_cardscale);
	}

	override void A_HDUPKGive()
	{
		let storage = MerchantRewardStorage.Get();
		storage.Money += Amount;
		Amount = 0;
		Super.A_HDUPKGive();
	}

	Default
	{
		HDUPK.PickupMessage "You picked up a debit card loaded with cash! Looks like the previous owner won't need it anymore.";
		HDUPK.Amount 100;
		Scale 0.25;
	}

	States
	{
		Spawn:
			MCRD A -1;
			Stop;
	}
}
